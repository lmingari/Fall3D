#!/bin/csh -f
#--------------------------------------------------------------------------
#   Script to convert NCEP2 reanalysis grib files to netCDF.
#
#   Requires:
#     - wgrib and the utility program Grib2nc
#   Data:
#     - http://nomad3.ncep.noaa.gov/ncep_data/index.html
#   Format:
#     - GRIB1
#   Pressure levels:
#     - 17, starting at 10mb (31 km aprox)
#     - 10 20 30 50 70 100 150 200 250 300 400 500 600 700 850 925 1000
#   Comments:
#     - Files(00 cycle):  pressure levels: pgb.YYYYMM
#     - Single files (for different months) must be concatened before running
#       this script:   cat pgb.* > problemname.grib
#     - The files grb2dYYYYMM surface data are NOT used because
#       are given in a different mesh (no possible to merge in a single
#       nc file without interpolating).
#--------------------------------------------------------------------------
#
# File: Scripts/Script-ncep2-to-nc.  Generated from Script-ncep2-to-nc.in by configure.
#
set SCRIPTNAM=Script-ncep2-to-nc
set EXEPROG=Grib2nc
set wgrib=NOTFOUND
set prefix=/home/mingari/Fall3d
set exec_prefix=${prefix}
set EXEPATH=${exec_prefix}/bin/$EXEPROG             # Path of the executable
set RUNDIR=/home/mingari/Fall3d/Runs
set DATADIR=/home/mingari/Fall3d/Data
set VER=7.0
set PROBLEMNAME=$1
set GRIBCONFDIR=/home/mingari/Fall3d/Utilities/Grib2nc/config
#
########################################################################
#
#    ************** USER DEFINED PARAMETERS ******************
#
#  List of timesteps to decode (YYMMDDHH)
#
set timesteps="13040100 13040106 13040112"     # put as many as required
#
#  File names and paths
#
set Gribcnf=${GRIBCONFDIR}/ncep2.configure                  # Model configure file
set GRIBNAME=${DATADIR}/ncep2-grib/${PROBLEMNAME}.grib               # Original grib file (concatened)
set NCNAME=${DATADIR}/ncep2-nc/${PROBLEMNAME}.ncep2.nc               # nc output file
set LOGNAME=${DATADIR}/ncep2-nc/Grib2nc.log                          # Grib2nc log output file
#
#  You don't need to change anything below this line
#
#############################################################
#
if ( ${PROBLEMNAME} == '' ) then
  echo 'Usage: '${SCRIPTNAM}' PROBLEMNAME'
  exit
else
  echo ' '
  echo '*** Launching '${SCRIPTNAM}' for '${PROBLEMNAME}' ***'
  echo '*** Time steps: '${timesteps}
  echo ' '
    endif
#
if ( ! -r ${Gribcnf} ) then
  echo 'Error: cannot open file:' ${Gribcnf}
  exit
endif
#
set TMP=${DATADIR}/ncep2-tmp       # tmp folder
set format=-bin                    # -bin or -text
#
#  NOT necessary to change anything below this line
#
#
#  1. Decoding using wgrib
#
set run_wgrib=1
if( ${run_wgrib} == 1 ) then
#
#  1.1. Creates the temporary folder
#
if( -d ${TMP} ) then
  rm -r ${TMP}
endif
mkdir ${TMP}
#
#   1.2. Creates the inventory file
#
set inv_file=${TMP}/all.inv
${wgrib} -s ${GRIBNAME} > ${inv_file}
#
#   1.3. Extract the date YYMMDDHH from first record
#
set nt=-1
foreach step ( ${timesteps} )
  set nt=`expr ${nt} + 1`
  if( ${nt} == 0 ) then
     set date1 = ${step}
  endif
end
#
#   1.4. Decode variables
#
set time = 0
foreach time_instant ( $timesteps )
#
#  2D variables (only geopotential at surface. The rest are in surface file (not used)
#
  foreach var ( HGT:sfc )
    set OUTPUT=${TMP}/${var}.${time}
    if( ${var} == HGT:sfc) then
       set var = "HGT"
       set var2 = "sfc"
    endif
    grep ":${var}" ${inv_file} | grep "${var2}" | grep "${time_instant}" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
  end
#
#   3D variables
#
  foreach var ( UGRD VGRD VVEL HGT TMP RH )        # List of 3D variables
    foreach lev ( 10 20 30 50 70 100 150 200 250 300 400 500 600 700 850 925 1000 )
      set OUTPUT=${TMP}/${var}.${time}.${lev}
      grep ":${var}:" ${inv_file} | grep ":${lev} mb" | grep "${time_instant}" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
    end
  end
#
set time = `expr $time + 6`
end
endif
#
#  2. Runs Grib2nc
#
set inv_file=${TMP}/all.inv
#
#  Gets the number of steps and modifies the ncep1.configure accordingly
#
set nt=-1
foreach step ( ${timesteps} )
  set nt=`expr ${nt} + 1`
end
set GribcnfTMP=${Gribcnf}.tmp
cat ${Gribcnf} | awk -v nt2="$nt" '{ \
    if(index($0,"! Number of time steps")!=0) {print "nt "nt2"        ! Number of time steps"} \
    else {print $0} \
    }' > ${GribcnfTMP}
mv ${GribcnfTMP} ${Gribcnf}
#
set run_Grib2nc=1
if( ${run_Grib2nc} == 1 ) then
 ${EXEPATH} ${date1} ${LOGNAME} ${Gribcnf} ${NCNAME} ${TMP} ncep1
endif
#
#  3. Removes the TMP folder
#
rm -rf ${TMP}
