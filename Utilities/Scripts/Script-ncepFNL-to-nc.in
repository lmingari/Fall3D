#!/bin/csh -f
#--------------------------------------------------------------------------
#   Script to convert NCEP GFS final analysis (1 deg) grib files to netCDF.
#
#   Requires:
#     - wgrib and the utility program Grib2nc
#   Data:
#     - http://rda.ucar.edu/datasets/ds083.2/
#   Format:
#     - GRIB1
#   Pressure levels:
#     - 26, starting at 10mb (31 km aprox)
#     - 10 20 30 50 70 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 925 950 975 1000
#   Comments:
#     - File names (00cycle) fnl_20130527_00_00_c, fnl_20130527_06_00_c, etc
#     - Single files for each time instant must be concatened before running
#       this script:   cat fnl_* > problemname.grib
#
#--------------------------------------------------------------------------
#
# File: @configure_input@
#
set SCRIPTNAM=Script-ncepFNL-to-nc
set EXEPROG=Grib2nc
set wgrib=@WGRIB@
set prefix=@prefix@
set exec_prefix=@exec_prefix@
set EXEPATH=@bindir@/$EXEPROG             # Path of the executable
set RUNDIR=@RUNDIR@
set DATADIR=@DATADIR@
set VER=@PACKAGE_VERSION@
set PROBLEMNAME=$1
set GRIBCONFDIR=@GRIBCONFDIR@
#
########################################################################
#
#    ************** USER DEFINED PARAMETERS ******************
#
#  List of timesteps to decode (YYMMDDHH)
#
set timesteps="13052700 13052706"     # put as many as required
#
set PROBLEMNAME=$1
if ( ${PROBLEMNAME} == '' ) then
  echo 'Usage: '${SCRIPTNAM}' PROBLEMNAME'
  exit
else
  echo ' '
  echo '*** Launching '${SCRIPTNAM}' for '${PROBLEMNAME}' ***'
  echo '*** Time steps: '${timesteps}
  echo ' '
endif
#
#  File names and paths
#
set Gribcnf=${GRIBCONFDIR}/ncepFNL.configure                # Model configure file
set GRIBNAME=${DATADIR}/ncepFNL-grib/${PROBLEMNAME}.grib             # Original GFS at 1 deg grib2 file (concatened)
set NCNAME=${DATADIR}/ncepFNL-nc/${PROBLEMNAME}.ncepFNL.nc           # GFS nc output file
set LOGNAME=${DATADIR}/ncepFNL-nc/Grib2nc.log                        # Grib2nc log output file
#
#  You don't need to change anything below this line
#
#############################################################
#
if ( ${PROBLEMNAME} == '' ) then
  echo 'Usage: Script-ncepFNL-to-nc PROBLEMNAME'
  exit
else
  echo ' '
  echo '*** Launching Script-ncepFNL-to-nc for '${PROBLEMNAME}' ***'
  echo '*** Time steps: '${timesteps}
  echo ' '
    endif
#
if ( ! -r ${Gribcnf} ) then
  echo 'Error: cannot open file:' ${Gribcnf}
  exit
endif
#
set TMP=${DATADIR}/ncepFNL-tmp     # tmp folder
set format=-bin                    # -bin or -text
#
#  NOT necessary to change anything below this line
#
#
#  1. Decoding using wgrib
#
set run_wgrib=1
if( ${run_wgrib} == 1 ) then
#
#  1.1. Creates the temporary folder
#
if( -d ${TMP} ) then
  rm -r ${TMP}
endif
mkdir ${TMP}
#
#   1.2. Creates the inventory file
#
set inv_file=${TMP}/all.inv
${wgrib} -s ${GRIBNAME} > ${inv_file}
#
#   1.3. Extract the date YYMMDDHH from the file list
#
set nt=-1
foreach step ( ${timesteps} )
  set nt=`expr ${nt} + 1`
  if( ${nt} == 0 ) then
     set date1 = ${step}
  endif
end
#
#   1.4. Decode variables
#
set time = 0
foreach time_instant ( $timesteps )
#
#   2D variables
#
  foreach var ( UGRD:10 VGRD:10 TMP:surface TMP:2 LAND HGT:surface PRES:surface HPBL )   # List of 2D variables
    set OUTPUT=${TMP}/${var}.${time}
    if( ${var} == UGRD:10) then
       set var = "UGRD"
       set var2 = "10 m above gnd"
    else if( ${var} == VGRD:10) then
       set var = "VGRD"
       set var2 = "10 m above gnd"
    else if( ${var} == TMP:2) then
       set var = "TMP"
       set var2 = "2 m above gnd"
    else if( ${var} == TMP:surface) then
       set var = "TMP"
       set var2 = "sfc"
    else if( ${var} == LAND) then
       set var = "LAND"
       set var2 = "sfc"
    else if( ${var} == HGT:surface) then
       set var = "HGT"
       set var2 = "sfc"
    else if( ${var} == PRES:surface) then
       set var = "PRES"
       set var2 = "sfc"
    else if( ${var} == HPBL) then
       set var = "HPBL"
       set var2 = "sfc"
    endif
#
    grep ":${var}" ${inv_file} | grep "${var2}" | grep "${time_instant}" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
#
  end
#
#   3D variables
#
  foreach var ( UGRD VGRD VVEL HGT TMP RH )        # List of 3D variables
    foreach lev ( 10 20 30 50 70 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 925 950 975 1000 )
        set OUTPUT=${TMP}/${var}.${time}.${lev}
#
        grep ":${var}:" ${inv_file} | grep ":${lev} mb" | grep "${time_instant}" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
    end
  end
#
set time = `expr $time + 6`
end
endif
#
#  2. Runs Grib2nc
#
set inv_file=${TMP}/all.inv
#
#  Gets the number of steps and modifies the gfs05deg.configure accordingly
#
set nt=-1
foreach step ( ${timesteps} )
  set nt=`expr ${nt} + 1`
end
set GribcnfTMP=${Gribcnf}.tmp
cat ${Gribcnf} | awk -v nt2="$nt" '{ \
    if(index($0,"! Number of time steps")!=0) {print "nt "nt2"        ! Number of time steps"} \
    else {print $0} \
    }' > ${GribcnfTMP}
mv ${GribcnfTMP} ${Gribcnf}
#
set run_Grib2nc=1
if( ${run_Grib2nc} == 1 ) then
 ${EXEPATH} ${date1} ${LOGNAME} ${Gribcnf} ${NCNAME} ${TMP} ncepFNL
endif
#
#  3. Removes the TMP folder
#
rm -rf ${TMP}
