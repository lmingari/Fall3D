#!/bin/csh -f
#--------------------------------------------------------------------------
#   Script to convert GFS 0.5 deg grib files to netCDF.
#
#   Requires:
#     - wgrib and the utility program Grib2nc
#   Data:
#     - http://nomad3.ncep.noaa.gov/ncep_data/index.html
#   Format:
#     - GRIB1
#   Pressure levels:
#     - 26, starting at 10mb (31 km aprox)
#     - 10 20 30 50 70 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 925 950 975 1000
#   Comments:
#     - File names (00cycle) gfs.t00z.master.grbf00, gfs.t00z.master.grbf03, etc
#     - Single files for each time instant must be concatened before running
#       this script:   cat gfs.t00z.master.grbf* > problemname.grib
#
#--------------------------------------------------------------------------
#
# File: @configure_input@
#
set SCRIPTNAM=Script-gfs05deg-to-nc
set EXEPROG=Grib2nc
set wgrib=@WGRIB@
set prefix=@prefix@
set exec_prefix=@exec_prefix@
set EXEPATH=@bindir@/$EXEPROG             # Path of the executable
set RUNDIR=@RUNDIR@
set DATADIR=@DATADIR@
set VER=@PACKAGE_VERSION@
set PROBLEMNAME=$1
set GRIBCONFDIR=@GRIBCONFDIR@
#
########################################################################
#
#    ************** USER DEFINED PARAMETERS ******************
#
#  List of timesteps to decode
#
set timesteps="0 3"     # put as many as required, e.g "0 3 6 9 12 15...."
#
#  File names and paths
#
set Gribcnf=${GRIBCONFDIR}/gfs05deg.configure               # Model configure file
set GRIBNAME=${DATADIR}/gfs05deg-grib/${PROBLEMNAME}.grib            # Original GFS at 1 deg grib2 file (concatened)
set NCNAME=${DATADIR}/gfs05deg-nc/${PROBLEMNAME}.gfs05deg.nc         # GFS nc output file
set LOGNAME=${DATADIR}/gfs05deg-nc/Grib2nc.log                       # Grib2nc log output file
#
#  You don't need to change anything below this line
#
#############################################################
#
if ( ${PROBLEMNAME} == '' ) then
  echo 'Usage: '${SCRIPTNAM}' PROBLEMNAME'
  exit
else
  echo ' '
  echo '*** Launching '${SCRIPTNAM}' for '${PROBLEMNAME}' ***'
  echo '*** Time steps: '${timesteps}
  echo ' '
    endif
#
if ( ! -r ${Gribcnf} ) then
  echo 'Error: cannot open file:' ${Gribcnf}
  exit
endif
#
set TMP=${DATADIR}/gfs05deg-tmp    # tmp folder
set format=-bin                      # -bin or -text
#
#  1. Decoding using wgrib
#
set run_wgrib=1
if( ${run_wgrib} == 1 ) then
#
#  1.1. Creates the temporary folder
#
if( -d ${TMP} ) then
  rm -r ${TMP}
endif
mkdir ${TMP}
#
#   1.2. Creates the inventory file
#
set inv_file=${TMP}/all.inv
${wgrib} -s ${GRIBNAME} > ${inv_file}
#
#   1.3. Extract the date YYMMDDHH from first record
#
set date1=`grep '1:0:' ${inv_file} | awk '{ date1=substr($0,7,8); print date1; }'`
#
#   1.4. Decode variables
#
foreach time ( $timesteps )
#
#   2D variables
#
  foreach var ( UGRD:10 VGRD:10 TMP:surface TMP:2 LAND HGT:surface PRES:surface HPBL )   # List of 2D variables
    set OUTPUT=${TMP}/${var}.${time}
    if( ${var} == UGRD:10) then
       set var = "UGRD"
       set var2 = "10 m above gnd"
    else if( ${var} == VGRD:10) then
       set var = "VGRD"
       set var2 = "10 m above gnd"
    else if( ${var} == TMP:2) then
       set var = "TMP"
       set var2 = "2 m above gnd"
    else if( ${var} == TMP:surface) then
       set var = "TMP"
       set var2 = "sfc"
    else if( ${var} == LAND) then
       set var = "LAND"
       set var2 = "sfc"
    else if( ${var} == HGT:surface) then
       set var = "HGT"
       set var2 = "sfc"
    else if( ${var} == PRES:surface) then
       set var = "PRES"
       set var2 = "sfc"
    else if( ${var} == HPBL) then
       set var = "HPBL"
       set var2 = "sfc"
    endif
    if( ${time} == 0) then
       grep ":${var}" ${inv_file} | grep "${var2}" | grep ":anl" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
    else
       grep ":${var}" ${inv_file} | grep "${var2}" | grep ":${time}hr" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
    endif
  end
#
#   3D variables
#
  foreach var ( UGRD VGRD VVEL HGT TMP RH )        # List of 3D variables
    foreach lev ( 10 20 30 50 70 100 150 200 250 300 350 400 450 500 550 600 650 700 750 800 850 900 925 950 975 1000 )
        set OUTPUT=${TMP}/${var}.${time}.${lev}
        if( ${time} == 0) then
          grep ":${var}:${lev} mb" ${inv_file} | grep ":anl" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
        else
          grep ":${var}:${lev} mb" ${inv_file} | grep ":${time}hr" | ${wgrib} -i ${GRIBNAME} ${format} -o ${OUTPUT}
        endif
    end
  end
#
end
endif
#
#  2. Runs Grib2nc
#
set inv_file=${TMP}/all.inv
set date1=`grep '1:0:' ${inv_file} | awk '{ date1=substr($0,7,8); print date1; }'`
#
#  Gets the number of steps and modifies the gfs05deg.configure accordingly
#
set nt=-1
foreach step ( ${timesteps} )
  set nt=`expr ${nt} + 1`
end
set GribcnfTMP=${Gribcnf}.tmp
cat ${Gribcnf} | awk -v nt2="$nt" '{ \
    if(index($0,"! Number of time steps")!=0) {print "nt "nt2"        ! Number of time steps"} \
    else {print $0} \
    }' > ${GribcnfTMP}
mv ${GribcnfTMP} ${Gribcnf}
#
set run_Grib2nc=1
if( ${run_Grib2nc} == 1 ) then
 ${EXEPATH} ${date1} ${LOGNAME} ${Gribcnf} ${NCNAME} ${TMP} gfs05deg
endif
#
#  3. Removes the TMP folder
#
rm -rf ${TMP}
